<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_HController" Id="{aa256504-dbce-4b10-a9b8-ece4e52985f5}" SpecialFunc="None">
    <Declaration><![CDATA[
FUNCTION_BLOCK FB_HController IMPLEMENTS I_HController
VAR
    iMotionAxisDetAngle : I_MotionAxis;
    iCalculateHFromAngle : I_CalculateHFromAngle;
    stMotionStageAxisDetAngle : REFERENCE TO ST_MotionStage;
    stMotionStageAxisDetH : REFERENCE TO ST_MotionStage;
    bDetHMoveAbsoluteBusy : REFERENCE TO BOOL;

    fHPositionMeasured : LREAL;
    fHPositionSetpoint : LREAL;

    fHPositionGain : LREAL;
    fHPositionAdjustmentIntegration : LREAL;

    nEncoderCounts AT %Q* : ULINT; // nm/count

    rtMoveCmdTrigger : R_TRIG;
    ftResetTrigger : F_TRIG;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[
fHPositionMeasured := iCalculateHFromAngle.CalculateH(
    Radians := DEG_TO_RAD(iMotionAxisDetAngle.MeasuredPosition)
);

nEncoderCounts := TO_ULINT(fHPositionMeasured * 1E6);

fHPositionSetpoint := iCalculateHFromAngle.CalculateH(
    Radians := DEG_TO_RAD(iMotionAxisDetAngle.SetpointPosition)
);

rtMoveCmdTrigger(CLK := stMotionStageAxisDetAngle.bExecute);

IF rtMoveCmdTrigger.Q THEN
    stMotionStageAxisDetH.bMoveCmd := TRUE;
    stMotionStageAxisDetH.fPosition := iCalculateHFromAngle.CalculateH(
        Radians := DEG_TO_RAD(stMotionStageAxisDetAngle.fPosition)
    );
    stMotionStageAxisDetH.fVelocity := stMotionStageAxisDetH.stAxisParameters.fVeloMaximum *
        stMotionStageAxisDetAngle.fVelocity / stMotionStageAxisDetAngle.stAxisParameters.fVeloMaximum;
END_IF

ftResetTrigger(CLK := bDetHMoveAbsoluteBusy);

IF ftResetTrigger.Q THEN
    stMotionStageAxisDetAngle.bReset := TRUE;
    stMotionStageAxisDetH.bReset := TRUE;
END_IF
]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{411c437b-e74e-4a25-8ab5-d2a195e88b34}">
      <Declaration><![CDATA[
METHOD FB_Init: BOOL
VAR_INPUT
    bInitRetains: BOOL; // TRUE: the retain variables are initialized (reset warm / reset cold)
    bInCopyCode: BOOL;  // TRUE: the instance will be copied to the copy code afterward (online change)

    iMotionAxisDetAngle : I_MotionAxis;
    iCalculateHFromAngle : I_CalculateHFromAngle;
    stMotionStageAxisDetAngle : REFERENCE TO ST_MotionStage;
    stMotionStageAxisDetH : REFERENCE TO ST_MotionStage;
    bDetHMoveAbsoluteBusy : REFERENCE TO BOOL;

END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
THIS^.iMotionAxisDetAngle := iMotionAxisDetAngle;
THIS^.iCalculateHFromAngle := iCalculateHFromAngle;
THIS^.stMotionStageAxisDetAngle REF= stMotionStageAxisDetAngle;
THIS^.stMotionStageAxisDetH REF= stMotionStageAxisDetH;
THIS^.bDetHMoveAbsoluteBusy REF= bDetHMoveAbsoluteBusy;
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_HController">
      <LineId Id="3" Count="27" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_HController.FB_Init">
      <LineId Id="3" Count="5" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>